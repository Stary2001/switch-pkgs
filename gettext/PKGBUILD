# Maintainer: Ezekiel Bethel <stary@9net.org>

pkgname=switch-gettext-runtime
pkgver=0.19.8
pkgrel=1
pkgdesc='gettext port (for Nintendo Switch homebrew development)'
arch=('any')
url='https://www.gnu.org/software/gettext/'
license=('GPL')
options=(!strip libtool staticlibs)
depends=()
source=('https://ftp.gnu.org/pub/gnu/gettext/gettext-0.19.8.tar.gz'
)

sha256sums=('3da4f6bd79685648ecf46dab51d66fcdddc156f41ed07e580a696a38ac61d48f'
)

build() {
  cd gettext-$pkgver/gettext-runtime

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  # Disable building programs & tests.
  sed -i 's/bin_PROGRAMS = .*//' Makefile.am

  sed -i 's/HAVE_POSIX_SIGNALBLOCKING=0/HAVE_POSIX_SIGNALBLOCKING=1/g' configure
  sed -i 's/HAVE_POSIX_SIGNALBLOCKING=0/HAVE_POSIX_SIGNALBLOCKING=1/g' gnulib-m4/signalblocking.m4

  ./configure --prefix=$PORTLIBS_PREFIX --host=aarch64-none-elf \
    --disable-shared --enable-static --disable-threads --disable-java --disable-native-java --disable-libasprintf

  make
}

package() {
  cd gettext-$pkgver/gettext-runtime

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install
}