# Maintainer: Ezekiel Bethel <stary@9net.org>

pkgname=switch-libpcre
pkgver=8.42
pkgrel=1
pkgdesc='libpcre port (for Nintendo Switch homebrew development)'
arch=('any')
url='https://www.pcre.org/'
license=('BSD')
options=(!strip libtool staticlibs)
depends=()
source=('https://ftp.pcre.org/pub/pcre/pcre-8.42.tar.gz'
)

sha256sums=('69acbc2fbdefb955d42a4c606dfde800c2885711d2979e356c0636efde9ec3b5'
)

build() {
  cd pcre-$pkgver

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  # Disable building programs & tests.
  sed -i 's/bin_PROGRAMS += .*//' Makefile.am
  sed -i 's/TESTS += .*//' Makefile.am
  sed -i 's/noinst_PROGRAMS += .*//' Makefile.am

  ./configure --prefix=$PORTLIBS_PREFIX --host=aarch64-none-elf \
    --disable-shared --enable-static

  make
}

package() {
  cd pcre-$pkgver

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install
}