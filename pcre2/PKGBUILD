# Maintainer: Ezekiel Bethel <stary@9net.org>

pkgname=switch-libpcre
pkgver=10.31
pkgrel=1
pkgdesc='libpcre2 port (for Nintendo Switch homebrew development)'
arch=('any')
url='https://www.pcre.org/'
license=('BSD')
options=(!strip libtool staticlibs)
depends=()
source=('https://ftp.pcre.org/pub/pcre/pcre2-10.31.tar.gz'
)

sha256sums=('e11ebd99dd23a7bccc9127d95d9978101b5f3cf0a6e7d25a1b1ca165a97166c4')

build() {
  cd pcre2-$pkgver

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  # Disable building programs & tests.
  sed -i 's/bin_PROGRAMS += .*//' Makefile.am
  sed -i 's/TESTS += .*//' Makefile.am
  sed -i 's/noinst_PROGRAMS += .*//' Makefile.am
	
  autoreconf
  ./configure --prefix=$PORTLIBS_PREFIX --host=aarch64-none-elf \
    --disable-shared --enable-static --enable-pcre2-16 --enable-pcre2-32

  make
}

package() {
  cd pcre2-$pkgver

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install
}
